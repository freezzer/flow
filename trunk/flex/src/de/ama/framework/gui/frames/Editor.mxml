<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas implements="de.ama.framework.gui.frames.IEditor"
           xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:frames="de.ama.framework.gui.frames.*"
           width="100%" height="100%"
	       initialize="init()"
	       creationComplete="onCreationComplete()"

           >

	<mx:Script><![CDATA[
        import de.ama.framework.command.Command;
        import de.ama.framework.command.SaveBoCommand;
        import de.ama.framework.data.Data;
        import de.ama.framework.data.SelectionModel;
        import de.ama.framework.util.*;

        import mx.controls.Button;

        private var _data:Data=null;
        private var _label:String=null;

        public function init():void {
            addCommands();
            addPanels();
        }

        public function onCreationComplete():void {
            buttonbar._callBack = new Callback(this, editorButtonHandler);
        }

        public function addCommand(command:Command):void {
            buttonbar.addCommand(command);
        }


        public function setData(data:Data):void {
            if (data == null) {
                data = createData();
            }
            
            _data = data;
            var childs:Array = getChildren();
            var i:int;
            var len:int = childs.length;
            var p:EditPanel = null;
            for (i = 0; i < len; i++) {
                if (childs[i] is EditPanel) {
                    p = childs[i];
                    p.setData(Data(data.getValue(p.path)));
                }
            }

            label = _label;

        }
        
        override public function set label(txt:String):void {
            _label = txt;
            super.label = txt;       	
        }
        
        override public function get label():String {
	    	if(_data!=null){
		        if (Util.isEmpty(_label)) {
		            return _data.getName();
		        }
		
		        if(_label.indexOf("{")>=0 && _label.indexOf("}")>0) {
		           var pre:String = _label.split("{")[0];
		           var post:String = _label.split("}")[1];
		           var lpath:String = _label.substring(_label.indexOf("{")+1,_label.indexOf("}"));
		           var prop:String = _data.getProperty(lpath);
		           return pre+prop+post;
		        }
	        }
	        return _label;
        }


        public function getData():Data {
        	return _data;
        }

        public function getSelectionModel():SelectionModel {
            return new SelectionModel(getData());
        }


        protected function contextMenuTriggered(event:ContextMenuEvent):void
        {
        }

        protected function menuItemSelectHandler(event:ContextMenuEvent):void {
        }

        private function toolBarButtonHandler(event:MouseEvent):void {
            var button:Button = Button(event.currentTarget);
            var command:Command = Command(button.data);
            command.start(this);
        }

        private function editorButtonHandler(event:MouseEvent):void {
            var button:Button = Button(event.currentTarget);
            var command:Command = Command(button.data);
            command.start(this);
        }


        /////////////////////////////////// Overwrites ///////////////////////////////////////////

        public function createData():Data {
            return null;
        }

        public function addCommands():void {
            addCommand(new SaveBoCommand());
        }

        public function addPanels():void {
        }




        ]]></mx:Script>

    <mx:VBox width="100%" height="100%" paddingBottom="0" paddingRight="0" paddingLeft="0" paddingTop="0" verticalGap="0" >

        <mx:Box color="0xffffff" width="100%" height="100%" paddingBottom="1" paddingRight="1" paddingLeft="1"
                        paddingTop="1" horizontalGap="8" backgroundColor="#F0F0F0" >

            <mx:Canvas id="contenPanel"  borderStyle="solid" borderThickness="2" cornerRadius="4" width="100%" height="100%"/>

        </mx:Box>

        <frames:CommandButtonBar id="buttonbar" horizontalAlign="right" useLabels="true"  verticalAlign="middle"/>
    </mx:VBox>
</mx:Canvas>
