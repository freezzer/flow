<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
           xmlns:frames="de.ama.framework.gui.frames.*"
           initialize="init()" keyDown="onKeyPressed(event)">


    <mx:Script><![CDATA[
        import de.ama.framework.command.Command;
        import de.ama.framework.command.MenuInvoker;
        import de.ama.services.BootstrapDefault;

        import de.ama.services.Environment;

        import mx.collections.ArrayCollection;
        import mx.controls.MenuBar;
        import mx.core.Container;
        import mx.events.MenuEvent;

        private function onKeyPressed(event:KeyboardEvent):void {
            var key:String = String.fromCharCode(event.charCode).toLowerCase();
            if (event.altKey && event.ctrlKey) {
                if (key == 'd') {
                    Environment.designMode = true;
                }
            }
            if(Environment.designer!=null){
                Environment.designer.keyHandler(event);
            }

        }

        private var mainTabs:AdvanceTabNavigator = null;
        private var mainMenuBar:MenuBar;
        [Bindable]
        private var menuStructure:ArrayCollection = new ArrayCollection();

        protected function executeGeneratedBootstrap():void {
        }

        protected function initializeMenu():void {
        }


        public function init():void {

            mainMenuBar = new MenuBar();
            mainMenuBar.labelField = "label";
            mainMenuBar.iconField = "icon";
            mainMenuBar.height = 23;
            mainMenuBar.setStyle("top", 3);
            mainMenuBar.setStyle("left", 3);
            mainMenuBar.setStyle("right", 3);
            mainMenuBar.addEventListener("itemClick", handleMenuClick);
            mainMenuBar.dataProvider = menuStructure;
            addChild(mainMenuBar);

            mainTabs = new AdvanceTabNavigator();
            mainTabs.setStyle("width", "100%");
            mainTabs.tabCloseEnable = true;
            mainTabs.tabDragEnable = true;
            mainTabs.setStyle("top", "30");
            mainTabs.setStyle("bottom", "0");
            mainTabs.setStyle("left", "3");
            mainTabs.setStyle("right", "3");
            //            mainTabs.setStyle("backgroundColor", "#F0F0F0");
            addChild(mainTabs);

            var bootstrapDefault:BootstrapDefault = new BootstrapDefault();
            bootstrapDefault.execute();

            executeGeneratedBootstrap();

            initializeMenu();
        }

        public function addMenu(label:String):ArrayCollection {
            var menu:Object = new Object();
            menu.label = label;
            menu.children = new ArrayCollection();
            menuStructure.addItem(menu);
            return menu.children;
        }

        public function addContent(child:Container):void {
            mainTabs.addChild(child);
            mainTabs.selectedChild = child;
        }


        protected function handleMenuClick(evt:MenuEvent):void {
            var cmd:Command = Command(evt.item);
            cmd.start(new MenuInvoker());
        }


        ]]></mx:Script>

</mx:Canvas>
